apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore/liko12.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        upload {
            storeFile rootProject.file("keystore/liko12.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias 'upload'
            keyPassword keystoreProperties['keyPassword']
        }
        standalone {
            storeFile rootProject.file("keystore/liko12.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias 'standalone'
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    ndkVersion '21.4.7075529'
    defaultConfig {
        applicationId 'me.ramilego4game.liko12'
        versionCode System.getenv("VERSION_CODE") ? System.getenv("VERSION_CODE").toInteger() : 1
        versionName System.getenv("VERSION_NAME") ? System.getenv("VERSION_NAME").toString() : 'CUSTOM'
        minSdkVersion 16
        targetSdkVersion 32
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'mode', 'recording', 'destribution'
    productFlavors {
        normal {
            dimension 'mode'
        }
        embed {
            dimension 'mode'
        }
        record {
            dimension 'recording'
        }
        noRecord {
            dimension 'recording'
        }
        googlePlay {
            dimension 'destribution'
            versionNameSuffix '-googleplay'
            signingConfig signingConfigs.upload
        }
        standalone {
            dimension 'destribution'
            versionNameSuffix '-standalone'
            applicationIdSuffix '.standalone'
            signingConfig signingConfigs.standalone
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    api 'androidx.multidex:multidex:2.0.1'
    api fileTree(dir: 'libs', include: ['*.jar'])
    api 'androidx.appcompat:appcompat:1.4.0'
    api project(':love')
}
